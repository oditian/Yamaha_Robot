'======================
CALL *CalculateTray(1,2,3,100,3,4)  REM 参数1 第1个点 参数2:第2个点 参数3:第3个点 参数4:新托盘起始点位 参数5:行 参数6:列
HOLD
SUB *CalculateTray(Point1%,Point2%,Point3%,Tray%,Row%,Column%)  REM 参数1 第一个点 参数2:第二个点 参数3:第3个点 参数4:新托盘起始点位  参数5:行 参数6:列
FOR I%=1 TO Row%*Column%
    P[Tray%+I%]=0.0 0 0 0 0 0 
NEXT
RowSpacing_num%=Row%-1
ColumnSpacing_num%=Column%-1
Posx1!=LOC1(P[Point1%])
Posy1!=LOC2(P[Point1%])
Posx2!=LOC1(P[Point2%])
Posy2!=LOC2(P[Point2%])
Posx3!=LOC1(P[Point3%])
Posy3!=LOC2(P[Point3%])
IF RowSpacing_num%=0 THEN
   RowSpacing_x!=0
   RowSpacing_y!=0
ELSEIF RowSpacing_num%>0 THEN
   RowSpacing_x!=(Posx3!-Posx1!)/RowSpacing_num%
   RowSpacing_y!=(Posy3!-Posy1!)/RowSpacing_num%
ENDIF
IF ColumnSpacing_num%=0 THEN
   ColumnSpacing_x!=0
   ColumnSpacing_y!=0
ELSEIF ColumnSpacing_num%>0 THEN
   ColumnSpacing_x!=(Posx2!-Posx1!)/ColumnSpacing_num%
   ColumnSpacing_y!=(Posy2!-Posy1!)/ColumnSpacing_num%
ENDIF
FOR I%=1 TO Row% 
    FOR J%=1 TO Column%
        CacuNum%=J%+(I%-1)*Column%
        P[Tray%+CacuNum%]=P[Point1%]
        LOC1(P[Tray%+CacuNum%])=LOC1(P[Point1%])+(J%-1)*ColumnSpacing_x!+(I%-1)*RowSpacing_x!
        LOC2(P[Tray%+CacuNum%])=LOC2(P[Point1%])+(J%-1)*ColumnSpacing_y!+(I%-1)*RowSpacing_y!
    NEXT
NEXT
END SUB